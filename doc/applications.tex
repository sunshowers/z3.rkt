\section{Our Experience}

We have successfully used \texttt{z3.rkt} in a number of applications, from
constraint-solving puzzles as illustrated in Section~\ref{sec:interactive}, to
verification and counterexample generation for functional programs. Along the
way, we have been pleasantly surprised by how well the power of Z3 and the
expressiveness of Racket combine. In this section we discuss the lessons we
have learnt and a few ideas we have explored.

\subsection{Quantified Formulas Are Hard}
\label{sec:quantified}

Z3 and other SMT solvers support lists and other recursive types. Z3 provides
only basic support for lists: \texttt{insert} (cons), \texttt{head}, and
\texttt{tail}. Further, Z3's macros are substitutions and do not support
recursion. This makes it challenging to define functions that operate over the
entirety of an arbitrary-length list.

Our first thought was to use a universal quantifier, as in
Section~\ref{sec:derived}. Here is an example of a function that calculates
the length of an integer list:

\begin{verbatim}
(declare-fun len ((List Int)) Int)
(assert (forall ((xs (List Int)))
  (ite (= xs nil)
       (= (len xs) 0)
       (= (len xs) (+ 1 (len (tail xs)))))))
\end{verbatim}

There is a drawback with this approach: solving quantified assertions in Z3
requires model-based quantifier instantiation (MBQI)~\cite{mbqi}. MBQI, while
powerful, can also be very slow. In our experience, it is very easy to write a
quantified formula that Z3's MBQI engine fails to solve in reasonable
time\footnote{In general, it is hard to deal with quantified formulas
containing even linear arithmetic, because there is no sound and complete
decision procedure for them\cite{halpern91}.

Z3 has another way to solve quantified assertions, called
\textit{E-matching}~\cite{e-matching}. E-matching uses patterns based on
ground terms to instantiate quantifiers. We have not yet explored this
approach.}.

So avoiding quantified formulas altogether seems like a good idea, but how do
we do that? The easiest way is to unroll and bound the recursion to a desired
depth~\cite{sat-recursive}. One way to do this is to define macros
\texttt{len-0}, \texttt{len-1}, \texttt{len-2}, \ldots \texttt{len-N}, where
each \texttt{len-k} returns the length of the list if it is less than or equal
to \texttt{k}, and \texttt{k} otherwise.

\begin{verbatim}
(define-fun len-0 ((xs (List Int)))
  0)

(define-fun len-1 ((xs (List Int)))
  (ite (= xs nil)
       0
       (+ 1 (len-0 (tail xs)))))

(define-fun len-2 ((xs (List Int)))
  (ite (= xs nil)
       0
       (+ 1 (len-1 (tail xs)))))
...
\end{verbatim}

Our system makes defining a series of macros like this very easy.

\begin{verbatim}
(define (make-length n)
  (smt:define-fun len ((xs (List Int))) Int
    (if (zero? n)
        0
        (ite/s (=/s xs nil/s)
               0
               (let ([sublen (make-length (sub1 n))])
                 (+/s 1 (sublen (tail/s xs)))))))
  len)
\end{verbatim}

\texttt{(make-length 5)} returns an SMT function that works for lists of up to
length 5, and returns 5 for anything bigger than that. Note how freely the
Racket \texttt{if} and \texttt{let} forms are mixed into the SMT body. These
constructs are evaluated at definition time, meaning that this definition
reduces to the series of macros defined above, and is much more concise.

It is easy to define other bounded recursive functions along the same lines
that reverse lists, concatenate them, filter them on a predicate and much
more. Using these building blocks we can now verify properties of recursive
functions.

\subsection{Verifying Recursive Functions}

For this section we will work with a simple but non-trivial
example:~\textit{quicksort}. A simple functional implementation of quicksort
might look like the following:

\begin{verbatim}
(define (qsort lst)
  (if (null? lst)
      null
      (let*
       ([pivot (car lst)]
        [rest (cdr lst)]
        [left (qsort (filter (lambda (x) (<= x pivot)) rest))]
        [right (qsort (filter (lambda (x) (> x pivot)) rest))])
        (append left (cons pivot right)))))
\end{verbatim}

This definition is correct, but what if the programmer mistakenly types in
\texttt{<} instead of \texttt{<=}, or perhaps uses \texttt{>=} instead of
\texttt{>}? We note that (a) for a correct implementation, the length of the
output will always be the same as that of the input, and that (b) in either
buggy case, the length of the output will be different whenever a pivot is
repeated in the rest of the list. So comparing the two lengths is a good
property to verify.

Using the method discussed in Section~\ref{sec:quantified}, we can write
\texttt{make-qsort} that generates bounded recursive versions of
\texttt{qsort}. Then we can verify the length property for all input lists up
to a certain length \texttt{n}.

\begin{verbatim}
(smt:with-context
 (smt:new-context)
 (define qsort (make-qsort n))
 ; adding 1 to the maximum length is enough to show inequality
 (define len (make-length (add1 n)))
 (smt:declare-fun xs () (List Int))
 ; set a bound on the length
 (smt:assert (<=/s (len xs) n))
 ; prove the length property by asserting its negation
 (smt:assert (not/s (=/s (len xs) (len (qsort xs)))))
 (smt:check-sat))
\end{verbatim}

Proving a property is done by checking that its negation is unsatisfiable. A
quicksort works \textit{correctly}\footnote{Here correctness is in the context
of the property we are considering, i.e. the length of the output.} for lists
up to length \texttt{n} iff the above code returns \texttt{'unsat}. For
quicksorts that are buggy (\texttt{'sat}), we can find a counterexample using
\texttt{(smt:eval xs)} and \texttt{(smt:eval (qsort xs))}. For $\mathtt{n}=4$
on a buggy quicksort with filters \texttt{<=} and \texttt{>=}, Z3 returned us
the counterexample with input \texttt{'(-3 -2 -1 -2)}, which as expected
contains a repeated element.

In this approach, there is nothing specific to quicksort: this can be easily
generalised to other functions that operate on lists and other data
structures. The properties to prove can be more complex, such as whether a
given sorting algorithm is stable. The only limits are computational
constraints and the user's imagination.
